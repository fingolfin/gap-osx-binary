%
\Chapter{Fixed-point-free automorphism groups}
%

The functions described in this chapter are purely group-theoretic and
are meant to provide solvable fixed-point-free automorphism groups acting on
abelian groups (Frobenius groups with abelian Frobenius kernel and solvable
Frobenius complement)
for the construction of centralizer nearrings, planar nearrings, designs,
and so on.

The classification of fixed-point-free automorphism groups in types I - IV 
follows Zassenhaus' papers and \cite{Wolf:Spaces}.
%J. A. Wolf, Spaces of Constant curvature.
The fixed-point-free automorphism groups acting on abelian groups are
constructed from fixed-point-free representations as described in 
\cite{Mayr:Representations}.
%Peter Mayr, Fixed-point-free Representations over Fields of Prime Characteristic, Johannes Kepler University Linz - Reports of the Mathematical Institutes, 554, 2000.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\Section{Fixed-point-free automorphism groups and Frobenius groups}


\>IsFpfAutomorphismGroup( <phi>, <G> )

An automorphism group $\Phi$ of a group $G$ acts fixed-point-free (fpf) on $G$
if $\Phi$ has more than 1 element and no automorphism in $\Phi$ except the
identity mapping has a fixed point besides the group identity of $G$.

$\Phi$ is fpf on $G$, iff the semidirect product of
$G$ and $\Phi$, with $\Phi$ acting naturally on $G$, is a Frobenius group.

The function `IsFpfAutomorphismGroup' returns the according value 
`true' or `false' for a group of automorphisms <phi> on the group <G>.

\beginexample
    gap> C9 := CyclicGroup( 9 );
    <pc group of size 9 with 2 generators>
    gap> a := GroupHomomorphismByFunction( C9, C9, x -> x^-1 );;
    gap> phi := Group( a );;
    gap> Size( phi );
    2
    gap> IsFpfAutomorphismGroup( phi, C9 );
    true
\endexample

\>FpfAutomorphismGroupsMaxSize( <G> )

`FpfAutomorphismGroupsMaxSize' returns a list with integers <kmax>
and <dmax> where <kmax> is an upper bound for the size of an fpf
automorphism group on the group <G>; for example, the order of <G> is
congruent to 1 modulo <kmax> and <kmax> is odd for nonabelian groups <G>. 
The order of any fpf automorphism group <phi> on <G> divides <kmax>.

Let <phi> be a metacyclic fpf automorphism group acting on <G>.
Then <phi> has a cyclic normal subgroup whose index in <phi> divides <dmax>.
Thus, if <dmax> is 1, then <G> admits cyclic fpf automorphism
groups only.

\beginexample
    gap> G := ElementaryAbelianGroup( 49 );;
    gap> FpfAutomorphismGroupsMaxSize( G );
    [ 48, 2 ]
    gap> C15 := CyclicGroup( 15 );;
    gap> FpfAutomorphismGroupsMaxSize( C15 );
    [ 2, 1 ]
\endexample

\>FrobeniusGroup( <phi>, <G> )

`FrobeniusGroup' constructs the semidirect product of <G> with the 
fpf automorphism group <phi> of <G> with the multiplication
$(a,g)*(b,h)=(ab,g^ah)$ by using the function `SemidirectProduct'.
Thus a Frobenius group with Frobenius kernel <G> and Frobenius complement
<phi> where the action of <phi> on <G> is the natural action of automorphisms
on the group is returned.

The unique Frobenius group with kernel
$G = (Z_{3})^2\times(Z_{5})^2$
and quaternion complement is constructed as follows:

\beginexample
    gap> aux := FpfAutomorphismGroupsMetacyclic( [3,3,5,5], 4, -1 ); 
    [ [ [ [ f1, f2, f3, f4 ] -> [ f1^2, f2^3, f3*f4, f3*f4^2 ], 
              [ f1, f2, f3, f4 ] -> [ f2^4, f1, f4^2, f3 ] ] ], 
      <pc group of size 225 with 4 generators> ]
    gap> phi := Group( aux[1][1] );
    <group with 2 generators>
    gap> G := aux[2];
    <pc group of size 225 with 4 generators>
    gap> FrobeniusGroup( phi, G );
    <pc group of size 1800 with 7 generators>
\endexample

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\Section{Fixed-point-free representations}


\>IsFpfRepresentation( <matrices>, <F>  )

Let $\pi$ be a representation of the group $\Phi$ over the finite field $F$.
If for all $\varphi\in\Phi$ except for the identity the matrix $\pi(\varphi)$
does not have $1$ as an eigenvalue, then $\pi$ is said to be fpf.

Let $\pi$ be an fpf representation of $\Phi$ over $F$ with degree
$d$. Then $\pi$ is faithful, the order of $\Phi$ and the characteristic of
$F$ are coprime and $\pi$ is a sum of irreducible faithful fpf
$F$-representations.
The matrix group $\pi(\Phi)$ acts fpf on the vectorspace $F^d$.

For a list of $d\times d$ matrices, <matrices>, over the field <F>,
the function `IsFpfRepresentation' returns `true' if the  
group generated by <matrices> acts fpf on the $d$-dimensional
vectorspace over <F>, and `false' otherwise.

\beginexample
    gap> F := GF(5);;
    gap> A := [[2,0],[0,3]]*One(F);
    [ [ Z(5), 0*Z(5) ], [ 0*Z(5), Z(5)^3 ] ]
    gap> IsFpfRepresentation( [A], F );
    true
\endexample

\>DegreeOfIrredFpfRepCyclic( <p>, <m> )

returns the degree of the irreducible fpf representations of
the cyclic group of order <m> over GF(<p>), where <m> and <p> are coprime.

Note, that all irreducible fpf representations of the cyclic group
of order <m> over GF(<p>) have the same degree, the multiplicative order 
of <p> modulo <m>, `OrderMod( p, m )'.

\beginexample
    gap> DegreeOfIrredFpfRepCyclic( 5, 9 );
    6
\endexample
 
\>DegreeOfIrredFpfRepMetacyclic( <p>, <m>, <r> )

returns the degree of the irreducible fpf representations of
the metacyclic group $\Phi$ determined by parameters <m> and <r> over
GF(<p>). If the parameters are not feasible, then an error is returned.
See `FpfRepresentationsMetacyclic' for a presentation of this
group.

All irreducible fpf representations of the metacyclic group
over GF(<p>) have the same degree, namely the size of multiplicative group
generated by <p> and <r> modulo <m>.

We determine the degree of the irreducible fpf representation of the 
quaternion group over GF(5):
 
\beginexample
    gap> DegreeOfIrredFpfRepMetacyclic( 5, 4, -1 );
    2
\endexample
 

\>DegreeOfIrredFpfRep2( <p>, <m>, <r>, <k> )

returns the degree of the irreducible fpf representations of
the type-II-group $\Phi$ determined by parameters <m>, <r>, and <k> over
GF(<p>). If the parameters are not feasible or if the parameters describe
the presentation of a metacyclic group, then an error is returned.
See `FpfRepresentations2' for a presentation of this group.

All irreducible fpf representations of $\Phi$ over GF(<p>) have
the same degree, namely the size of the multiplicative group
generated by <p>, <r>, and <k> modulo <m>.

We determine the degree of the irreducible fpf representation of the smallest,
not metacyclic type-2-group (order 120) over the field GF(7): 
 
\beginexample
    gap> DegreeOfIrredFpfRep2( 7, 30, 11, -1 );    
    8
\endexample
 
\>DegreeOfIrredFpfRep3( <p>, <m>, <r> )

returns the degree of the irreducible fpf representations of
the type-III-group $\Phi$ determined by parameters <m> and <r> over
GF(<p>). If the parameters are not feasible, then an error is returned.
See `FpfRepresentations3' for a presentation of this
group.

All irreducible fpf representations of this group
over GF(<p>) have the same degree.

We determine the degree of the irreducible fpf representation of SL(2,3)
over GF(5):
 
\beginexample
    gap> DegreeOfIrredFpfRep3( 5, 3, 1 );                                  
    2
\endexample

\>DegreeOfIrredFpfRep4( <p>, <m>, <r>, <k> )

returns the degree of the irreducible fpf representations of
the type-IV-group $\Phi$ determined by parameters <m>, <r>, and <k> over
GF(<p>). If the parameters are not feasible, then an error is returned.
See `FpfRepresentations4' for a presentation of this group.

All irreducible fpf representations of $\Phi$ over GF(<p>) have
the same degree.

We determine the degree of the irreducible fpf representation of the smallest
type-4-group, the binary octahedral group of order 48, over $GF(5)$:
 
\beginexample
    gap> DegreeOfIrredFpfRep4( 5, 3, 1, -1 );   
    4
\endexample
 
\>FpfRepresentationsCyclic( <p>, <m> )

Let $a$ generate a cyclic group of order <m>. 
%determines the nonequivalent irreducible fpf representations of a
%cyclic group of order <m> over GF(<p>).
For <p> and <m> coprime `FpfRepresentationsCyclic'
returns a list of matrices $\{ A^i | i$ in $indexlist \}$ over GF(<p>)
as well as the list $indexlist$.
For all $i$ in $indexlist$ the representation $a \mapsto A^i$  
is irreducible and fpf.
The $A^i$ with $i$ in $indexlist$ describe all irreducible fpf
representations up to equivalence; each irreducible fpf
representation is equivalent to one $a \mapsto A^i$ and no two representations
$a \mapsto A^i$, $a \mapsto A^j$ with $i\neq j$ and $i,j$ in $indexlist$ are
equivalent.

Note, that every faithful irreducible representation of a cyclic group 
is fpf. The number of nonequivalent faithful irreducible
representations over GF(<p>) is given as $\phi(m)/d$, where the degree $d$ is
given as the multiplicative order of <p> modulo <m> and $\phi(m)$ denotes
the number of residues coprime to $m$.

We determine the irreducible matrix representations of the cyclic group of
size 8 over $GF(5)$:

\beginexample
    gap> aux := FpfRepresentationsCyclic( 5, 8 );
    [ [ [ [ Z(5)^3, Z(5)^2 ], [ Z(5), Z(5) ] ], 
          [ [ Z(5)^2, Z(5) ], [ Z(5)^0, Z(5)^0 ] ] ], [ 1, 7 ] ]
    gap> mats := aux[1];
    [ [ [ Z(5)^3, Z(5)^2 ], [ Z(5), Z(5) ] ], 
      [ [ Z(5)^2, Z(5) ], [ Z(5)^0, Z(5)^0 ] ] ]
    gap> indexlist := aux[2];
    [ 1, 7 ]
\endexample
 
\>FpfRepresentationsMetacyclic( <p>, <m>, <r> )

 Let $\Phi$ be a metacyclic group (i.e., $\Phi$ has a cyclic normal subgroup
 with cyclic factor) admitting an fpf representation. Then $\Phi$
 fulfills one of the following two presentations, I or II. Both presentations
 are determined by integers $m$ and $r$ satisfying certain conditions:
 
 Type I. Presentation of an fpf metacyclic group $\Phi$ with all
 Sylow subgroups cyclic.
 Let $m$ and $r$ satisfy the following conditions: 

\beginlist
\item{(a)} $m$ and $r$ are coprime. 
\item{(b)} Let $n$ be the multiplicative order of $r$ modulo $m$.
 Then each prime divisor of $n$ divides $m$.
\item{(c)} Let $m'$ be maximal such that $m'$ divides $m$ and $m'$ is coprime
 to $n$. Then $r = 1$ mod $(m/m')$.
\endlist

 Type II. Presentation of an fpf metacyclic group $\Phi$ with
 generalized quaternion 2-Sylow subgroup.
 Let $m$ and $r$ satisfy the following conditions: 

\beginlist
\item{(a)} $m$ and $r$ are coprime. 
\item{(b)} Let $n$ be the multiplicative order of $r$ modulo $m$.
 Then $n$ is $2$ times an odd number and each prime divisor of $n$ divides $m$.
\item{(c)} Let $s$ be maximal such that $2^s$ divides $m$. Then $2^s\geq 4$
 and $r = -1$ mod $2^s$. 
\item{(d)} Let $m'$ be maximal such that $m'$ divides $m/2$ and $m'$ is
 coprime to $n/2$. Then $r = 1$ mod $(m/m')$. 
\endlist

 Then the group $\Phi$ with 2 generators $a,b$ satisfying the relations
 $a^m = 1, b^n = a^{m'}, b^{-1}ab = a^r$
 is metacyclic and fpf and has size $mn$.

 A group satisfying presentation I is of type I in the notation of Zassenhaus,
 Wolf; presentation II gives a type-II-group.

Let <m>, <r> be as above, and let <p> coprime to <m>.
Additionally, we require that <m> does not divide <r>-1. (Otherwise,
$\Phi = \langle a,b | a^m = 1, b^n = a^{m'}, b^{-1}ab = a^r \rangle$
would be cyclic.) Then
`FpfRepresentationsMetacyclic'
returns a list of matrices $\{ (A^i,B_i) | i$ in $indexlist \}$ over GF(<p>)
as well as the list $indexlist$. The GF(<p>)-representations determined by
$a \mapsto A^i$ and $b \mapsto B_i$ are all irreducible and fpf
representations of
$\Phi = \langle a,b | a^m = 1, b^n = a^{m'}, b^{-1}ab = a^r \rangle$
up to equivalence.

We determine the irreducible matrix representation of the quaternion group
(parameters $m = 4, r = -1$) over $GF(7)$:

\beginexample
    gap> aux := FpfRepresentationsMetacyclic( 7, 4, -1 );
    [ [ [ [ [ Z(7)^2, Z(7) ], [ Z(7), Z(7)^5 ] ], 
              [ [ 0*Z(7), Z(7)^3 ], [ Z(7)^0, 0*Z(7) ] ] ] ], [ 1 ] ]
    gap> mats := aux[1];
    [ [ [ [ Z(7)^2, Z(7) ], [ Z(7), Z(7)^5 ] ], 
          [ [ 0*Z(7), Z(7)^3 ], [ Z(7)^0, 0*Z(7) ] ] ] ]
\endexample

\>FpfRepresentations2( <p>, <m>, <r>, <k> )

 The presentation of a type-II-group which is not metacyclic is determined by
 integers $m,r,k$ satisfying the following conditions:
% (again in \cite{Wolf:Spaces}, 6.1.11, a slightly different notation is used):

\beginlist
\item{(a)} $m$ and $r$ are coprime, $m$ and $k$ are coprime.
\item{(b)} Let $n$ be the multiplicative order of $r$ modulo $m$.
 Then $n$ is $2$ times an odd number and each prime divisor of $n$ divides $m$.
\item{(c)} Let $m'$ be maximal such that $m'$ divides $m$ and $m'$ is coprime
 to $n$. Then $r = 1$ mod $(m/m')$.
\item{(d)} Let $2^{s-1}$ be maximal such that $2^{s-1}$ divides $m$. Define
 $l = -1$ mod $2^{s-1}$ and $l = 1$ mod $(nm/(2^{s-1}m'))$.
 Then $k = l$ mod $(m/m')$.
\item{(e)} The multiplicative order of $k$ modulo $m$ equals $2$ and
 $k\neq r^{(n/2)}$ mod $m$. 
\endlist

 Then the group $\Phi$ with generators $a,b,q$ satisfying the
 relations
 $a^m = 1, b^n = a^{m'}, b^{-1}ab = a^r$
 and furthermore
 $q^{-1}a q = a^k, q^{-1}b q = b^l$ is fpf
 of type II and has size $2mn$.

 $a,b$ generate a metacyclic group with all Sylow subgroups cyclic
 (see conditions (a), (b), (c)) of index $2$ in $\Phi$.

For <m>, <r>, <k> as above and <p> coprime to <m>
`FpfRepresentations2'
returns a list of matrices $\{ (A_i,B_i,Q_i) | i$ in $indexlist \}$ over
GF(<p>) as well as the list $indexlist$. The GF(<p>)-representations
determined by $a \mapsto A_i, b \mapsto B_i$ and $q \mapsto Q_i$ are all
irreducible, fpf representations of $\Phi$ upto equivalence.

We determine the irreducible matrix representations of the smallest
type-II-group which is not metacyclic (parameters m = 30, r = 11, k = -1,
size 120) over the field GF(11) and obtain 2 nonequivalent fpf representations,
each of degree 4:

\beginexample
    gap> DegreeOfIrredFpfRep2( 11, 30, 11, -1 );
    4
    gap> aux := FpfRepresentations2( 11, 30, 11, -1 );
    [ [ [ <block matrix of dimensions (2*2)x(2*2)>, 
              <block matrix of dimensions (2*2)x(2*2)>, 
              <block matrix of dimensions (2*2)x(2*2)> ], 
          [ <block matrix of dimensions (2*2)x(2*2)>, 
              <block matrix of dimensions (2*2)x(2*2)>, 
              <block matrix of dimensions (2*2)x(2*2)> ] ], [ 1, 13 ] ]
\endexample

\>FpfRepresentations3( <p>, <m>, <r> )

 A group $\Phi$ admitting an fpf representation is said to be of
 type III if $\Phi$ is the semidirect product of the quaternion group 
 and a metacyclic fpf group $H$ of odd size, with the quaternion
 group normal and $H$ permuting the $3$ subgroups of order $4$.   

 The presentation of a type-III-group is determined by integers $m$ and $r$,
 describing the metacyclic group $H$ and its action on the normal quaternion
 subgroup. The following conditions have to be satisfied for $m,r$:

\beginlist
\item{(a)} $3$ divides $m$; $m$ is odd; $m$ and $r$ are coprime.
\item{(b)} Let $n$ be the multiplicative order of $r$ modulo $m$.
 Then each prime divisor of $n$ divides $m$.
\item{(c)} Let $m'$ be maximal such that $m'$ divides $m$ and $m'$ is coprime
 to $n$. Then $r = 1$ mod $(m/m')$.
\endlist

 Let $p,q$ with relations $p^4 = 1, q^2 = p^2, q^{-1}pq = p^{-1}$ generate
 the quaternion group. Let $a,b$ generate a metacyclic group
 determined by $m$ and $r$ (See `FpfRepresentationsMetacyclic'). 

 If $3$ divides $n$, then let $a$ commute with $p,q$ and let
 $b^{-1}pb = q, b^{-1}qb = pq$.

 If $3$ does not divide $n$, then let $b$ commute with $p,q$ and let
 $a^{-1}pa = q, a^{-1}qa = pq$

 Then the group $\Phi$ with generators $p,q,a,b$ is of type III and
 has size $8mn$. 

For $r \neq 1$ mod $m$, `FpfRepresentations3' returns a list of matrices 
$\{ (P, Q, A_i,B_i) | i$ in $indexlist \}$ over GF(<p>) as well as the list
$indexlist$.

For $r = 1$ mod $m$, the group $H$ is cyclic and `FpfRepresentations3' returns
$\{ (P, Q, A_i) | i$ in $indexlist \}$ over GF(<p>) and $indexlist$.

The GF(<p>)-representations
determined by $p \mapsto P, q \mapsto Q$ and $a \mapsto A_i, b \mapsto B_i$
are all irreducible, fpf representations of $\Phi$ upto equivalence.

We determine the irreducible matrix representation of the smallest
type-III-group, namely SL(2,3), (parameters m = 3, r = 1, size 24) over the
field GF(5): 

\beginexample
    gap> aux := FpfRepresentations3( 5, 3, 1 );
    [ [ [ [ [ Z(5), 0*Z(5) ], [ 0*Z(5), Z(5)^3 ] ], 
              [ [ 0*Z(5), Z(5)^2 ], [ Z(5)^0, 0*Z(5) ] ], 
              [ [ Z(5)^3, Z(5)^0 ], [ Z(5), Z(5)^0 ] ] ] ], [ 1 ] ]
\endexample

\>FpfRepresentations4( <p>, <m>, <r>, <k> )

 A group $\Phi = \langle p,q,a,b,z\rangle$ admitting an fpf
 representation is said to be of type IV, if it has a normal subgroup
 $H = \langle p,q,a,b\rangle$ of type III and index 2.

 The presentation of a type-IV-group is determined by integers $m,r,k$
 satisfying the following conditions:

\beginlist
\item{(a)} Let $s$ be maximal such that $3^s$ divides $m$. Then
 $s\geq 1$; $m$ is odd; $m$ and $r$ are coprime.
\item{(b)} Let $n$ be the multiplicative order of $r$ modulo $m$.
 Then $3$ does not divide $n$; each prime divisor of $n$ divides $m$.
\item{(c)} Let $m'$ be maximal such that $m'$ divides $m$ and $m'$ is coprime
 to $n$. Then $r = 1$ mod $(m/m')$.
\item{(d)} $k = -1$ mod $3^s$, $k = 1$ mod $(m/m')$ and $k^2 = 1$ modulo $m$.
\endlist

 Let $p,q,a,b$ generate a type-III-group determined by $m,r$ with relations as
 given in Section `FpfRepresentations3'. Additionally, let
 $z^2 = p^2, z^{-1}pz = qp, z^{-1}qz = q^{-1}$ and
 $z^{-1}a z = a^k,z^{-1}b z = b$. 

 Then the group $\Phi$ with generators $p,q,a,b$ and $z$ is of type
 IV and has size $16mn$. 

For $r \neq 1$ mod $m$, `FpfRepresentations4' returns a list of matrices 
$\{ (P, Q, A_i,B_i, Z_i) | i$ in $indexlist \}$ over GF(<p>) as well as the
list $indexlist$.

For $r = 1$ mod $m$, the function `FpfRepresentations4' returns
$\{ (P, Q, A_i, Z_i) | i$ in $indexlist \}$ over GF(<p>) and $indexlist$.

The GF(<p>)-representations
determined by $p \mapsto P, q \mapsto Q$ and
$a \mapsto A_i, b \mapsto B_i, z \mapsto Z_i$
are all irreducible, fpf representations of $\Phi$ upto equivalence.

We determine the $2$ nonequivalent irreducible matrix representations of the
smallest type-IV-group (binary octahedral group, m = 3, r = 1, k = -1, size 48)
over the field GF(7): 

\beginexample
    gap> aux := FpfRepresentations4( 7, 3, 1, -1 );
    [ [ [ [ [ Z(7)^2, Z(7) ], [ Z(7), Z(7)^5 ] ], 
              [ [ 0*Z(7), Z(7)^3 ], [ Z(7)^0, 0*Z(7) ] ], 
              [ [ Z(7)^2, 0*Z(7) ], [ Z(7)^0, Z(7)^4 ] ], 
              [ [ Z(7)^5, Z(7) ], [ Z(7), Z(7)^2 ] ] ], 
          [ [ [ Z(7)^2, Z(7) ], [ Z(7), Z(7)^5 ] ], 
              [ [ 0*Z(7), Z(7)^3 ], [ Z(7)^0, 0*Z(7) ] ], 
              [ [ Z(7)^2, 0*Z(7) ], [ Z(7)^0, Z(7)^4 ] ], 
              [ [ Z(7)^2, Z(7)^4 ], [ Z(7)^4, Z(7)^5 ] ] ] ], 
      [ [ 1, 1 ], [ -1, 1 ] ] ]
\endexample

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\Section{Fixed-point-free automorphism groups}


\>FpfAutomorphismGroupsCyclic( <ints>, <m> )

If `AbelianGroup(<ints>)' admits a cyclic fpf automorphism group of size 
<m>, then `FpfAutomorphismGroupsCyclic' determines one representative for 
each conjugacy class of such fpf automorphism groups. Conjugacy is determined 
within the whole automorphism group of `AbelianGroup(<ints>)'. 

<ints> has to be a list of prime power integers and is sorted in the
function, according to the order
$p^i\leq q^j \Leftrightarrow p \< q$ or ($p=q$ and $j \< i$). 

`AbelianGroup(<ints>)' admits a cyclic fpf automorphism group
of size <m> iff the multiplicity of each prime power $p^i$ in <ints>
is divisible by `DegreeOfIrredFpfRepCyclic( p, m )'.

A list of generators of the nonconjugate fpf automorphism groups is returned
together with the group `AbelianGroup(<ints>)', on which the automorphisms act.
Here <ints> is sorted with the order above.

The generators, <as>, of the $2$ nonconjugate cyclic fpf
automorphism groups of order $4$ on $Z_{25}\times Z_{5}$ are computed
as follows:

\beginexample
    gap> aux := FpfAutomorphismGroupsCyclic( [25,5], 4 ); 
    [ [ [ f1, f3 ] -> [ f1^2*f2, f3^2 ], 
          [ f1, f3 ] -> [ f1^2*f2, f3^3 ] ], 
      <pc group of size 125 with 2 generators> ]
    gap> as := aux[1];
    [ [ f1, f3 ] -> [ f1^2*f2, f3^2 ], [ f1, f3 ] -> [ f1^2*f2, f3^3 ] ]
    gap> G := aux[2];
    <pc group of size 125 with 2 generators>
\endexample

\>FpfAutomorphismGroupsMetacyclic( <ints>, <m>, <r> )

If `AbelianGroup(<ints>)' admits a metacyclic fpf automorphism group 
determined by parameters <m> and <r> that is not cyclic
(see `FpfRepresentationsMetacyclic' for a presentation),
then `FpfAutomorphismGroupsMetacyclic' determines one
representative for each conjugacy class of such fpf automorphism groups. 
Conjugacy is determined within the whole automorphism group of 
`AbelianGroup(<ints>)'. 

<ints> has to be a list of prime power integers and is sorted in the
function, according to the order
$p^i\leq q^j \Leftrightarrow p \< q$ or $(p = q$ and $i\geq j)$. 

Moreover, the multiplicity of each prime power $p^i$ in <ints> has to be
divisible by `DegreeOfIrredFpfRepMetacyclic( p, m, r )', which is 
a multiple of the multiplicative order of $r$ modulo $m$.

A list of pairs of generators ($a,b$ satisfying $b^{-1}ab = a^r, a^m = 1$
and $b^n = a^{m'}$) of the nonconjugate fpf automorphism groups is returned
together with the group `AbelianGroup(<ints>)', on which the automorphisms act.
Here <ints> is sorted with the order above.

For $G = (Z_{3})^2\times(Z_{5})^2$ the quaternion
fpf automorphism group of size $8$ (parameters $m = 4, r = -1$) is computed as
follows:

\beginexample
    gap> aux := FpfAutomorphismGroupsMetacyclic( [3,3,5,5], 4, -1 );
    [ [ [ [ f1, f2, f3, f4 ] -> [ f1^2, f2^3, f3*f4, f3*f4^2 ], 
              [ f1, f2, f3, f4 ] -> [ f2^4, f1, f4^2, f3 ] ] ], 
      <pc group of size 225 with 4 generators> ]
    gap> fs := aux[1];
    [ [ [ f1, f2, f3, f4 ] -> [ f1^2, f2^3, f3*f4, f3*f4^2 ], 
          [ f1, f2, f3, f4 ] -> [ f2^4, f1, f4^2, f3 ] ] ]
    gap> phi := Group( fs[1] );
    <group with 2 generators>
    gap> G := aux[2];
    <pc group of size 225 with 4 generators>
\endexample

On $G = (Z_{7})^2\times(Z_{17})^2$ there are $2$ nonconjugate
fpf automorphism groups isomorphic to the generalized
quaternion group of size $16$ (parameters $m = 8, r = -1$): 

\beginexample
    gap> aux := FpfAutomorphismGroupsMetacyclic( [7,7,17,17], 8, -1 );;
    gap> fs := aux[1];
    [ [ [ f1, f2, f3, f4 ] -> [ f1^9, f2^2, f3^4*f4^2, f3*f4^6 ], 
          [ f1, f2, f3, f4 ] -> [ f2^16, f1, f3^4*f4^5, f3^5*f4^3 ] ], 
      [ [ f1, f2, f3, f4 ] -> [ f1^9, f2^2, f3^3*f4^5, f3^6*f4 ], 
          [ f1, f2, f3, f4 ] -> [ f2^16, f1, f3^3*f4^4, f3*f4^4 ] ] ]
    gap> phis := List( fs, Group );
    [ <group with 2 generators>, <group with 2 generators> ]
    gap> G := aux[2];
    <pc group of size 14161 with 4 generators>
\endexample

\>FpfAutomorphismGroups2( <ints>, <m>, <r>, <k> )

If `AbelianGroup(<ints>)' admits an fpf automorphism group of type II, 
determined by parameters <m>, <r>, <k> that is not metacyclic
(see `FpfRepresentations2' for a presentation), then `FpfAutomorphismGroups2' 
determines one representative for each conjugacy class of such fpf
automorphism groups. Conjugacy is determined within the whole automorphism 
group of `AbelianGroup(<ints>)'. 

<ints> has to be a list of prime power integers and is sorted in the
function, according to the order
$p^i\leq q^j \Leftrightarrow p \< q$ or $(p = q$ and $i\geq j)$. 

Note, that the degree of an irreducible fpf representation of a
type-II-group which is not metacyclic is divisible by $4$ and that the
multiplicity of each prime power $p^i$ in <ints> has to be divisible by
`DegreeOfIrredFpfRep2( p, m, r, k )'.

A list of triples of generators ($a,b,z$ satisfying $b^{-1}ab = a^r, a^m = 1$
and $z^{-1}az = a^{k}$) of the nonconjugate fpf automorphism groups is 
returned together with the group `AbelianGroup(<ints>)', on which the 
automorphisms act.
Here <ints> is sorted with the order above.

Upto conjugacy there is only one fpf automorphism group of type II with
parameters $m = 30, r = 11, k = -1$, size $120$ on the elementary abelian
group of size $11^4$:

\beginexample
    gap> aux := FpfAutomorphismGroups2( [11,11,11,11], 30, 11, -1 );
    [ [ [ [ f1, f2, f3, f4 ] -> [ f1^5*f2^4, f1^3*f2^10, f3^2*f4^8, 
                  f3^6*f4 ], 
              [ f1, f2, f3, f4 ] -> [ f1^3*f2^10, f1^10*f2^8, f3^8*f4, 
                  f3*f4^3 ], 
              [ f1, f2, f3, f4 ] -> [ f3^10, f4^10, f1, f2 ] ] ], 
      <pc group of size 14641 with 4 generators> ]
    gap> phi := Group( aux[1][1] );
    <group with 3 generators>
    gap> G := aux[2];
    <pc group of size 14641 with 4 generators>
\endexample

\>FpfAutomorphismGroups3( <ints>, <m>, <r> )

If `AbelianGroup(<ints>)' admits an fpf automorphism group of
type III determined by parameters <m> and <r> (see 
`FpfRepresentations3' for a presentation), then
`FpfAutomorphismGroups3' determines one representative for each
conjugacy class of such fpf automorphism groups. Conjugacy is
determined within the whole automorphism group of `AbelianGroup(<ints>)'. 

<ints> has to be a list of prime power integers and is sorted in the
function, according to the order
$p^i\leq q^j \Leftrightarrow p \< q$ or $(p = q$ and $i\geq j)$. 

Moreover, the multiplicity of each prime power $p^i$ in <ints> has to be
divisible by `DegreeOfIrredFpfRep3( p, m, r )', which is a multiple
of $2n$ where $n$ is the multiplicative order of $r$ modulo $m$.

A list of tuples of generators, <[p,q,a,b]>, ($p,q$ generating the quaternion
group, $a,b$ satisfying $b^{-1}ab = a^r, a^m = 1$ and $b^n = a^{m'}$) of the
nonconjugate fpf automorphism groups is returned together with the group 
`AbelianGroup(<ints>)', on which the automorphisms act.
Here <ints> is sorted with the order above.

For $G = (Z_{5})^2$ the fpf automorphism type-III-group
isomorphic to SL(2,3) is computed as follows (parameters $m = 3, r = 1$):

\beginexample
    gap> aux := FpfAutomorphismGroups3( [5,5], 3, 1 ); 
    [ [ [ [ f1, f2 ] -> [ f1^2, f2^3 ], [ f1, f2 ] -> [ f2^4, f1 ], 
              [ f1, f2 ] -> [ f1^3*f2, f1^2*f2 ] ] ], 
      <pc group of size 25 with 2 generators> ]
    gap> phi := Group( aux[1][1] );
    <group with 3 generators>
    gap> G := aux[2];
    <pc group of size 25 with 2 generators>
\endexample

\>FpfAutomorphismGroups4( <ints>, <m>, <r>, <k> )

If `AbelianGroup(<ints>)' admits an fpf automorphism group of type IV 
determined by parameters <m>, <r>, <k> (see `FpfRepresentations4' for a 
presentation), then `FpfAutomorphismGroups4' determines one representative 
for each conjugacy class of such fpf automorphism groups. Conjugacy is
determined within the whole automorphism group of `AbelianGroup(<ints>)'. 

<ints> has to be a list of prime power integers and is sorted in the
function, according to the order
$p^i\leq q^j \Leftrightarrow p \< q$ or $(p = q$ and $i\geq j)$. 

Moreover, the multiplicity of each prime power $p^i$ in <ints> has to be
divisible by `DegreeOfIrredFpfRep4( p, m, r )', which is a multiple
of $2n$ where $n$ is the multiplicative order of $r$ modulo $m$.

A list of tuples of generators, <[p,q,a,b,z]>, of the nonconjugate fpf 
automorphism groups is returned together with the group 
`AbelianGroup(<ints>)', on which the automorphisms act.
Here <ints> is sorted with the order above. 
If $r = 1$ mod $m$, then a list of tuples, <[p,q,a,z]>, is returned instead.

For $G = (Z_{7})^2$ the fpf automorphism type-IV-group isomorphic the binary
octahedral group of size 48 (parameters $m = 3, r = 1, k = -1$)
is computed as follows:

\beginexample
    gap> aux := FpfAutomorphismGroups4( [7,7], 3, 1, -1 );
    [ [ [ [ f1, f2 ] -> [ f1^2*f2^3, f1^3*f2^5 ], 
              [ f1, f2 ] -> [ f2^6, f1 ], [ f1, f2 ] -> [ f1^2, f1*f2^4 ], 
              [ f1, f2 ] -> [ f1^5*f2^3, f1^3*f2^2 ] ] ], 
      <pc group of size 49 with 2 generators> ]
    gap> phi := Group( aux[1][1] );
    <group with 4 generators>
    gap> G := aux[2];
    <pc group of size 49 with 2 generators>
\endexample

%%% Local Variables: 
%%% mode: latex
%%% TeX-master: t
%%% TeX-master: t
%%% End: 
