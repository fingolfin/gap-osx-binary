<?xml version="1.0" encoding="UTF-8"?>

<Chapter><Heading>Examples</Heading>
<Section><Heading>Right Engel elements</Heading>
An old problem in the context of Engel elements is the question:  Is
a right <M>n</M>-Engel element left <M>n</M>-Engel?  It is known
that the answer is no.  For details about the history of the
problem, see <Cite Key="NewmanNickel94"/>.  In this paper the
authors show that for <M>n>4</M> there are nilpotent groups with
right <M>n</M>-Engel elements no power of which is a left
<M>n</M>-Engel element.  The insight was based on computations with
the ANU NQ which we reproduce here.  We also show the cases
<M>5>n</M>.
<Example>
gap> LoadPackage( "nq" );
true
gap> ##  SetInfoLevel( InfoNQ, 1 );
gap> ##
gap> ##  setup calculation
gap> ##
gap> et := ExpressionTrees( "a", "b", "x" );
[ a, b, x ]
gap> a := et[1];; b := et[2];; x := et[3];;
gap> 
gap> ##
gap> ##  define the group for n = 2,3,4,5
gap> ##
gap> 
gap> rengel := LeftNormedComm( [a,x,x] );
Comm( a, x, x )
gap> G := rec( generators := et, relations := [rengel] );
rec( generators := [ a, b, x ], relations := [ Comm( a, x, x ) ] )
gap> ## The following is equivalent to:
gap> ##   NilpotentQuotient( : input_string := NqStringExpTrees( G, [x] ) )
gap> H := NilpotentQuotient( G, [x] );
Pcp-group with orders [ 0, 0, 0 ]
gap> LeftNormedComm( [ H.2,H.1,H.1 ] );
id
gap> LeftNormedComm( [ H.1,H.2,H.2 ] );
id
</Example>
This shows that each right 2-Engel element in a finitely generated
nilpotent group is a left 2-Engel element.  Note that the group above
is the largest nilpotent group generated by two elements, one of which
is right 2-Engel.  Every nilpotent group generated by an arbitrary
element and a right 2-Engel element is a homomorphic image of the
group <M>H</M>.
<Example>
gap> rengel := LeftNormedComm( [a,x,x,x] );
Comm( a, x, x, x )
gap> G := rec( generators := et, relations := [rengel] );
rec( generators := [ a, b, x ], relations := [ Comm( a, x, x, x ) ] )
gap> H := NilpotentQuotient( G, [x] );
Pcp-group with orders [ 0, 0, 0, 0, 0, 4, 2, 2 ]
gap> LeftNormedComm( [ H.1,H.2,H.2,H.2 ] );
id
gap> h := LeftNormedComm( [ H.2,H.1,H.1,H.1 ] );
g6^2*g7*g8
gap> Order( h );
4
</Example>
The element <M>h</M> has order <M>4</M>.  In a nilpotent group without
<M>2</M>-torsion a right 3-Engel element is left 3-Engel.
<Example>
gap> rengel := LeftNormedComm( [a,x,x,x,x] );
Comm( a, x, x, x, x )
gap> G := rec( generators := et, relations := [rengel] );
rec( generators := [ a, b, x ], relations := [ Comm( a, x, x, x, x ) ] )
gap> H := NilpotentQuotient( G, [x] );
Pcp-group with orders [ 0, 0, 0, 0, 0, 0, 0, 0, 2, 0, 12, 0, 5, 10, 2, 0, 30, 
  5, 2, 5, 5, 5, 5 ]
gap> LeftNormedComm( [ H.1,H.2,H.2,H.2,H.2 ] );
id
gap> h := LeftNormedComm( [ H.2,H.1,H.1,H.1,H.1 ] );
g9*g10^2*g11^10*g12^5*g13^2*g14^8*g15*g16^6*g17^10*g18*g20^4*g21^4*g22^2*g23^2
gap> Order( h );
60
</Example>
The previous calculation shows that in a nilpotent group without
<M>2,3,5</M>-torsion a right 4-Engel element is left 4-Engel. 
<Example>
gap> rengel := LeftNormedComm( [a,x,x,x,x,x] );
Comm( a, x, x, x, x, x )
gap> G := rec( generators := et, relations := [rengel] );
rec( generators := [ a, b, x ], relations := [ Comm( a, x, x, x, x, x ) ] )
gap> H := NilpotentQuotient( G, [x], 9 );
Pcp-group with orders [ 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 6, 0, 30, 
  0, 0, 30, 0, 3, 6, 0, 0, 10, 30, 0, 0, 0, 0, 30, 30, 0, 0, 3, 6, 5, 2, 0, 
  2, 408, 2, 0, 0, 0, 10, 10, 30, 10, 0, 0, 0, 3, 3, 3, 2, 204, 6, 6, 0, 10, 
  10, 10, 2, 2, 2, 0, 300, 0, 0, 18 ]
gap> LeftNormedComm( [ H.1,H.2,H.2,H.2,H.2,H.2 ] );
id
gap> h := LeftNormedComm( [ H.2,H.1,H.1,H.1,H.1,H.1 ] );;
gap> Order( h );
infinity
</Example>
Finally, we see that in a torsion-free group a right 5-Engel element
need not be a left 5-Engel element.
</Section>
</Chapter>
